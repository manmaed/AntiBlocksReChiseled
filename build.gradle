plugins {
    id 'java-library'
    id 'eclipse'
    id 'idea'
    id 'maven-publish'
    id 'net.neoforged.gradle.userdev' version "${neo_gradle}"
    id "me.modmuss50.mod-publish-plugin" version "0.4.5"
}

def ENV = System.getenv()
version = "${ENV.GITHUB_RUN_NUMBER}" ? "${mod_version}.${ENV.GITHUB_RUN_NUMBER}" : "${mod_version}.nobuildnumber"
group = project.maven_group

repositories {
    mavenLocal()
    maven {
        url "https://cursemaven.com"
    }
}

base {
    archivesName = project.archives_base_name + "-neo"
}

java.toolchain.languageVersion = JavaLanguageVersion.of(21)

runs {
    configureEach {
        systemProperty 'forge.logging.markers', 'REGISTRIES'
        systemProperty 'forge.logging.console.level', 'debug'
        modSource project.sourceSets.main
    }

    client {
        systemProperty 'forge.enabledGameTestNamespaces', project.archives_base_name
    }

    server {
        systemProperty 'forge.enabledGameTestNamespaces', project.archives_base_name
        programArgument '--nogui'
    }

    data {
        programArguments.addAll '--mod', project.archives_base_name, '--all', '--output', file('src/generated/resources/').getAbsolutePath(), '--existing', file('src/main/resources/').getAbsolutePath()
    }
}

sourceSets.main.resources { 
    srcDir 'src/generated/resources'
}

dependencies {
    implementation "net.neoforged:neoforge:${neo_version}"
    if(use_ctm.equals("true")) {
        runtimeOnly "curse.maven:ctm-${ctm_id}:${ctm_file_id}"
    }
    if(use_xewhy.equals("true")) {
        runtimeOnly "curse.maven:xycraft-${xy_id}:${xy_file_id}"
    }
    if(use_fct.equals("true")) {
        runtimeOnly "curse.maven:fct-${fct_id}:${fct_file_id}"
    }
    //runtimeOnly "curse.maven:emi-580555:5619579"
    //runtimeOnly "curse.maven:arc-419699:5553777"
    //runtimeOnly "curse.maven:tk-324888:5521502"
}
tasks.withType(ProcessResources).configureEach {
    var replaceProperties = [
            version: version
    ]
    inputs.properties replaceProperties

    filesMatching(['META-INF/neoforge.mods.toml']) {
        expand replaceProperties + [project: project]
    }
}

publishMods {
    dryRun = (providers.environmentVariable("CURSE_DEPLOY").getOrNull() || providers.environmentVariable("MODRINTH_DEPLOY").getOrNull()) == null
    file = jar.archiveFile
    modLoaders.add("neoforge")
    type = BETA
    changelog = "changeme"
    displayName = "${mod_name} v${project.version} - NeoForge - ${minecraft_version}"
    curseforge {
        accessToken = providers.environmentVariable("CURSE_DEPLOY")
        projectId = project.curseforge_id
        minecraftVersions.add("Java 21")
        minecraftVersions.add("${minecraft_version}")
        optional {
            slug = 'fusion-connected-textures'
        }
        optional {
            slug = 'ctm'
        }
    }
    modrinth {
        accessToken = providers.environmentVariable("MODRINTH_DEPLOY")
        projectId  = project.modrinth_id
        minecraftVersions.add("${minecraft_version}")
        optional {
            slug = 'fusion-connected-textures'
        }
    }

}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8' // Use the UTF-8 charset for Java compilation
}

idea {
    module {
        downloadSources = true
        downloadJavadoc = true
    }
}